---
layout: tutorial
title: "For Loops"
slug: for-loops
---

Now we know how to write code using functions, variables, and if statements. So far our code has worked by executing each line one after the other, so if we wanted to draw three circles, we'd have to write three separate calls to the `ellipse()` function.

This tutorial introduces `for` loops, which allow you to repeat work without repeating code.

## Patterns

Let's start with an example program:

```java
line(25, 0, 25, height);
line(50, 0, 50, height);
line(75, 0, 75, height);
```

This program draws three vertical lines: the first from position `25,0` to `25,height`; the second from position `50,0` to `50,height`; and the third from position `75,0` to `75,height`.

![3 lines](/tutorials/processing/for-loops-1.png)

In other words, we're changing the x position of the lines, and then drawing them from the top to the bottom of the window.

If we look closely, we can recognize a pattern in these three lines: the x position starts at `25`, then increase by `25`, and stops at `75`. When we have a pattern like this (start at a number, increase by a number, stop at a number), we can use `for` loops to follow that pattern to repeat code.

## New Syntax

To write a `for` loop, first type the keyword `for`, and then in parentheses `()` you give it three things:

- Declare a variable to keep track of your pattern, and initialize it to the number your pattern starts at: `int lineX = 25;`
- Write a test that evaluates to a `boolean` value of `false` whenever the pattern should stop: `lineX <= 75;`
- Reassign the variable so that it follows the pattern: `lineX = lineX + 25;` (which can be shortened to `lineX += 25`)

Then inside curly brackets `{}`, type the code that uses your variable to follow the pattern. Putting it all together, it looks like this:

```java
for(int lineX = 25; lineX <= 75; lineX += 25){
  line(lineX, 0, lineX, height);
}
```

This is new syntax, so let's go over it piece by piece:

- `int lineX = 25;` creates a **loop variable** with a value of `25`. This only happens **once**, at the very beginning of the loop.
- `lineX <= 75;` decides when to continue looping. This test is evaluated every step (which is called an **iteration**) of the pattern, at the **beginning** of the iteration. Whenever the test evaluates to `false`, the pattern is over and the loop stops iterating.
- `lineX += 25` updates the loop variable. This happens at the **end** of every iteration, after the body of the loop has run.
- `line(lineX, 0, lineX, height);` uses the `lineX` variable to draw a line in the pattern we gave it.

At the end of each iteration (when the code reaches the closing curly bracket `}`), a couple things happen:

- The code executes your reassignment statement to update the loop variable.
- Then the code jumps back to the beginning of the `for` loop!
- The check is evaluated, and if it's `true`, the body of the loop is executed again. If it's `false`, the loop exits and skips over the body.

This might seem like a lot to take in, but you can think about it as just a few steps:

- A loop variable is created and initialized to the first number in the pattern.
- The check is evaluated, and if it's `false`, the loop exits and its body is skipped. If it's `true`, then the body is executed.
- After the body executes, the loop variable is updated.
- Then the code jumps back to the beginning of the loop and performs the check again.

## The Benefit of For Loops

Our example is pretty simple, but `for` loops make it very easy to make more complicated patterns. For example, what if we wanted to draw `9` lines instead of `3` lines?

We could write code that draws each line manually:

```java
line(10, 0, 10, height);
line(20, 0, 20, height);
line(30, 0, 30, height);
line(40, 0, 40, height);
line(50, 0, 50, height);
line(60, 0, 60, height);
line(70, 0, 70, height);
line(80, 0, 80, height);
line(90, 0, 90, height);
```

But this code contains a pattern: the `x` value starts at `10`, increases by `10` each step, and ends at `90`. We can use a `for` loop to do this in just a couple lines:

```java
for(int lineX = 10; lineX <= 90; lineX += 10){
  line(lineX, 0, lineX, height);
}
```

This code uses a `for` loop to create a pattern where `lineX` starts at `10`, increases by `10` each iteration, and stops when `lineX` is greater than `90` (in other words, when `lineX <= 90` evaluates to `false`). During each step of the pattern, the code draws a vertical line using the `lineX` variable.

![9 lines](/tutorials/processing/for-loops-2.png)

What if we wanted to draw 50 lines? We can use a `for` loop to do the pattern for us instead of writing 50 lines of code:

```java
size(510, 100);
for(int lineX = 10; lineX <= 500; lineX += 10){
  line(lineX, 0, lineX, height);
}
```

![50 lines](/tutorials/processing/for-loops-3.png)

## Nested For Loops



## Homework

- Write a program that draws a horizontal gradient. Hint: draw `width` lines, and make each one a darker shade of gray.
- Write a program that makes every pixel in the window a different random color.
- Write a program that gives you the total of 1+2+3+4+...+100.
